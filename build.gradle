buildscript {
    dependencies {
        classpath 'org.yaml:snakeyaml:1.29'
    }
}


plugins {
    id 'java'
    id 'org.springframework.boot' version '2.5.3'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'checkstyle'
    id 'pmd'
    id "com.github.spotbugs" version '4.7.2'
    id 'jacoco'
}

group = 'org.genius'
version = '1.0-GENIUS-SNAPSHOT'
sourceCompatibility = '16'

ext {
    lombokVersion = "1.18.20"
    mapStructVersion = "1.4.2.Final"
    jacksonDataBindVersion = "2.12.5"
    springdocOpenApiUIVersion = "1.5.10"
    jjwtVersion = "0.11.1"
    logstashLogbackEncoder = "6.6"
    logbackAccessVersion = "1.2.5"

    generatedInvoiceGatewayWSSources = "$buildDir/generated/sources/jaxb/java/main"
}

jar {
    enabled = false
}

repositories {
    mavenCentral()
}

dependencies {
    // Spring Boot
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'

    // Lombok
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    runtimeOnly 'org.postgresql:postgresql'

    // Test dependencies
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    // Jackson
    implementation "com.fasterxml.jackson.core:jackson-databind:${jacksonDataBindVersion}"

    // JWT
    implementation "io.jsonwebtoken:jjwt-api:${jjwtVersion}"
    runtimeOnly "io.jsonwebtoken:jjwt-impl:${jjwtVersion}"
    runtimeOnly "io.jsonwebtoken:jjwt-jackson:${jjwtVersion}"

    runtimeOnly "net.logstash.logback:logstash-logback-encoder:${logstashLogbackEncoder}"
    implementation "ch.qos.logback:logback-access:${logbackAccessVersion}"

    // Mapstruct
    implementation 'org.mapstruct:mapstruct:1.4.2.Final'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.4.2.Final'


    implementation "org.springdoc:springdoc-openapi-ui:${springdocOpenApiUIVersion}"
    implementation 'org.springframework.ws:spring-ws-core'
    implementation 'javax.xml.soap:javax.xml.soap-api:1.4.0'
    implementation 'com.sun.xml.messaging.saaj:saaj-impl:1.5.1'

}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

checkstyle {
    toolVersion = '8.44'
    configFile = file("config/checkstyle/checkstyle.xml")
    showViolations = true
    maxWarnings = 2
    maxErrors = 1
}
checkstyleMain {
    source = 'src/main/java'
}
checkstyleTest {
    source = 'src/test/java'
}

pmd {
    toolVersion = "6.21.0"
}

spotbugs {
    reportsDir = file("$buildDir/reports/spotbugs/main")
    excludeFilter = file("config/spotbugs/findbugs-exclude.xml")
}
spotbugsMain {
    reports {
        html.enabled = true
    }
}
spotbugsTest {
    reports {
        html.enabled = true
    }
}

jacoco {
    toolVersion = '0.8.7'
    reportsDirectory = file("$buildDir/reports/jacoco")
}

